#compdef dock

# zsh completion for dock

local context state state_descr line
typeset -A opt_args

local -a _dock_commands
_dock_commands=(
  'reset:Reset Dock from a config file (or discovery)'
  'show:Print current Dock app names'
  'validate:Validate config and print planned actions'
  'backup:Write current Dock as YAML config (requires --file)'
)

_dock_profiles() {
  local dir="$HOME/.config/dock/profiles"
  [[ -d $dir ]] || return 1
  local -aU profiles
  profiles=(${dir}/*.yml(N:t:r) ${dir}/*.yaml(N:t:r))
  (( ${#profiles} )) || return 1
  _describe 'profile' profiles
}

_arguments -s -S -C \
  '--dry-run[Show what would be done, without changes]' \
  '(-f --file)'{-f,--file}'[Config file path]:file:_files' \
  '--profile=[Use config profile name]:profile:_dock_profiles' \
  '(-h --help)'{-h,--help}'[Show help]' \
  '--version[Print version]' \
  '1:command:->command' \
  '*::args:->args'

case $state in
  command)
    _describe 'command' _dock_commands
    ;;
  args)
    local cmd=${words[2]}
    case $cmd in
      backup)
        _arguments '*:file:_files'
        ;;
      *)
        return 0
        ;;
    esac
    ;;
esac

return 0
